module "vmss" {
  source = "../.."

  name                         = "acctestOVMSS-${random_integer.number.result}"
  location                     = azurerm_resource_group.test.location
  resource_group_id            = azurerm_resource_group.test.id
  platform_fault_domain_count  = 2
  sku_name                     = "Standard_D1_v2"
  instances                    = 1
  extension_operations_enabled = false

  os_profile = {
    linux_configuration = {
      computer_name_prefix = "testvm-${random_integer.number.result}"
      admin_username       = "myadmin"

      admin_ssh_key = [
        {
          username   = "myadmin"
          public_key = "ssh-rsa AAAAB3NzaC1yc2EAAAADAQABAAACAQDCsTcryUl51Q2VSEHqDRNmceUFo55ZtcIwxl2QITbN1RREti5ml/VTytC0yeBOvnZA4x4CFpdw/lCDPk0yrH9Ei5vVkXmOrExdTlT3qI7YaAzj1tUVlBd4S6LX1F7y6VLActvdHuDDuXZXzCDd/97420jrDfWZqJMlUK/EmCE5ParCeHIRIvmBxcEnGfFIsw8xQZl0HphxWOtJil8qsUWSdMyCiJYYQpMoMliO99X40AUc4/AlsyPyT5ddbKk08YrZ+rKDVHF7o29rh4vi5MmHkVgVQHKiKybWlHq+b71gIAUQk9wrJxD+dqt4igrmDSpIjfjwnd+l5UIn5fJSO5DYV4YT/4hwK7OKmuo7OFHD0WyY5YnkYEMtFgzemnRBdE8ulcT60DQpVgRMXFWHvhyCWy0L6sgj1QWDZlLpvsIvNfHsyhKFMG1frLnMt/nP0+YCcfg+v1JYeCKjeoJxB8DWcRBsjzItY0CGmzP8UYZiYKl/2u+2TgFS5r7NWH11bxoUzjKdaa1NLw+ieA8GlBFfCbfWe6YVB9ggUte4VtYFMZGxOjS2bAiYtfgTKFJv+XqORAwExG6+G2eDxIDyo80/OA9IG7Xv/jwQr7D6KDjDuULFcN/iTxuttoKrHeYz1hf5ZQlBdllwJHYx6fK2g8kha6r2JIQKocvsAXiiONqSfw== hello@world.com"
        }
      ]
    }
  }

  network_interface = [
    {
      name    = "TestNetworkProfile"
      primary = true

      ip_configuration = [
        {
          name      = "TestIPConfiguration"
          primary   = true
          subnet_id = azurerm_subnet.test.id

          public_ip_address = [{
            name                    = "TestPublicIPConfiguration"
            domain_name_label       = "test-domain-label"
            idle_timeout_in_minutes = 4
          }]
        }
      ]
    }
  ]

  os_disk = {
    storage_account_type = "Standard_LRS"
    caching              = "ReadWrite"
  }

  source_image_reference = {
    publisher = "Canonical"
    offer     = "0001-com-ubuntu-server-jammy"
    sku       = "22_04-lts"
    version   = "latest"
  }
}

resource "azapi_resource" "this" {
  type                             = module.vmss.azapi_header.type
  name                             = module.vmss.azapi_header.name
  location                         = module.vmss.azapi_header.location
  parent_id                        = module.vmss.azapi_header.parent_id
  tags                             = module.vmss.azapi_header.tags
  body                             = module.vmss.body
  ignore_null_property             = module.vmss.azapi_header.ignore_null_property
  sensitive_body                   = module.vmss.sensitive_body
  sensitive_body_version           = module.vmss.sensitive_body_version
  replace_triggers_external_values = module.vmss.replace_triggers_external_values
  locks                            = module.vmss.locks
  retry                            = module.vmss.retry

  dynamic "identity" {
    for_each = can(module.vmss.azapi_header.identity) ? [module.vmss.azapi_header.identity] : []
    content {
      type         = identity.value.type
      identity_ids = try(identity.value.identity_ids, null)
    }
  }

  dynamic "timeouts" {
    for_each = module.vmss.timeouts != null ? [module.vmss.timeouts] : []
    content {
      create = timeouts.value.create
      delete = timeouts.value.delete
      read   = timeouts.value.read
      update = timeouts.value.update
    }
  }
}

resource "azapi_update_resource" "update0" {
  type           = module.vmss.post_creation_updates[0].azapi_header.type
  name           = module.vmss.post_creation_updates[0].azapi_header.name
  parent_id      = module.vmss.post_creation_updates[0].azapi_header.parent_id
  body           = module.vmss.post_creation_updates[0].body
  sensitive_body = try(module.vmss.post_creation_updates[0].sensitive_body, null)
  depends_on     = [azapi_resource.this]
}
